plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.flywaydb.flyway' version '10.14.0'
    id 'checkstyle'
    id 'application'
}

group = 'de.unipassau'
version = '0.0.1-SNAPSHOT'
description = 'AllocationSystemBackend'

application {
    mainClass.set('de.unipassau.allocationsystem.AllocationSystemBackendApplication')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    
    // DB
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'com.h2database:h2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Jackson for JSON serialization in audit logs
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    
    // Apache POI for Excel file processing
    implementation 'org.apache.poi:poi:5.2.5'
    implementation 'org.apache.poi:poi-ooxml:5.2.5'

    // dev tools
    developmentOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    useJUnitPlatform()
}

// --- Checkstyle Configuration ---
checkstyle {
    toolVersion = '10.12.1'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    configDirectory = file("${rootDir}/config/checkstyle")
    ignoreFailures = false
    showViolations = true
    maxErrors = 0
    maxWarnings = 0
}

tasks.named('checkstyleMain').configure {
    reports {
        xml.required = true
        html.required = true
    }
}
tasks.named('checkstyleTest').configure {
    enabled = false
}
tasks.named('check') {
    dependsOn tasks.named('checkstyleMain')
}

//--- Checkstyle Configuration END ---
flyway {
    url = 'jdbc:h2:file:./data/allocdb;AUTO_SERVER=TRUE'
    user = 'sa'
    password = ''
}